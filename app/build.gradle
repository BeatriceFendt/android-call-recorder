apply plugin: 'com.android.application'

import com.android.build.OutputFile

android {
    compileSdkVersion 25
    buildToolsVersion '27.0.1'

    defaultConfig {
        applicationId "com.github.axet.callrecorder"
        minSdkVersion 9
        targetSdkVersion 23
        versionCode 124
        versionName "1.5.3"
    }
    signingConfigs {
        release {
            storeFile file(RELEASE_STORE_FILE)
            storePassword RELEASE_STORE_PASSWORD
            keyAlias RELEASE_KEY_ALIAS
            keyPassword RELEASE_KEY_PASSWORD
        }
    }
    buildTypes {
        debug {
            debuggable true
            jniDebuggable true
            minifyEnabled false
        }
        release {
            shrinkResources true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }
    applicationVariants.all { variant ->
        variant.outputs.each { output ->
            def abi = output.getFilter(OutputFile.ABI)
            if (abi == null)
                abi = "universal";
            output.outputFileName = org.apache.commons.io.FilenameUtils.getExtension(android.defaultConfig.applicationId) + "-" + abi + "-" + android.defaultConfig.versionName + "-" + variant.buildType.name + ".apk"
        }
    }
    splits {
        abi {
            enable true
            reset()
            include "x86", "armeabi-v7a", "mips"
            universalApk true
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.github.axet:android-audio-library:1.0.71' // compile project(':android-audio-library')
    testCompile 'junit:junit:4.12'
}
